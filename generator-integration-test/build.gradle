plugins {
    id "org.jetbrains.kotlin.jvm"
}

apply plugin: 'kotlin-kapt'

kapt.includeCompileClasspath = false

test {
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            logger.lifecycle("Integration test results: (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)")
            if (result.testCount == 0) {
                throw new IllegalStateException("No tests were found. Failing the build")
            }
        }
    }
}

kotlin {
    jvmToolchain(17)
}

dependencies {
    kapt project(":generator")
    annotationProcessor project(":generator")
    implementation project(':generator')

    implementation project(":generator-integration-test-api")

    compileOnly 'com.github.tomakehurst:wiremock-jre8:3.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.2.4'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.2.4'

    testImplementation('org.springframework.boot:spring-boot-starter-test:3.2.4') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'com.github.tomakehurst:wiremock-jre8:3.0.1'
    testImplementation 'com.oneeyedmen:okeydoke:2.0.3'
    testImplementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.14'
}
